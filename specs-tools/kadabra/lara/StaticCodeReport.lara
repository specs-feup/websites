/**
 * This aspect prints a report with information regarding:
 * 1. Total of files, classes, methods and calls
 * 2. Loop-related information, such as #innermost loops and largest loop nest
 * 3. Function calls information
 */
aspectdef StaticCodeReport

	println("[Static Report]");
	
	//Report variables
	var loops = [];
	var callInfo = new LaraObject();
	
	
	var num_files = 0;
	var num_classes = 0;
	var num_methods = 0;
	var num_calls = 0;
	var num_loops = 0;
	var num_loops_innermost = 0;
	var for_loops = 0;
	var for_innermost_loops = 0;
	var while_loops = 0;
	var while_innermost_loops = 0;
	var dowhile_loops = 0;
	var dowhile_innermost_loops = 0;
	var foreach_loops = 0;
	var foreach_innermost_loops = 0;

	var max_nest = -1;
	var max_nest_location = '';
	
	//Utility methods
	var withWidth = Utils.withWidth;
	var lineWidth = Utils.lineWidth;
	
	/* 
	 * Gather total of files, classes, methods and calls
	 */
	select file end
	apply
		num_files++;
	end

	select class end
	apply
		num_classes++;
	end
	
	select class.method end
	apply
		num_methods++;
	end
	
	/* 
	 * Count number of times a function is called and 
	 * the number of times a function calls other functions
	 */
	select class.method.call end
	apply 
		num_calls++; 
		
		callInfo.inc($call.name, 'callee');
		callInfo.inc($method.name, 'caller');
	end
	
	/* 
	 * Count number of loops (by type), the total of
	 * innermost loops, and the largest loop nest
	 */
	select file.class.method.loop end
	apply
		num_loops++;
		if($loop.type == "for") {
			for_loops++;
			if($loop.isInnermost) {
				num_loops_innermost++;
				for_innermost_loops++;
			}
		}
			if($loop.type == "while") {
			while_loops++;
			if($loop.isInnermost) {
				num_loops_innermost++;
				while_innermost_loops++;
			}
		}
		if($loop.type == "do-while") {
			dowhile_loops++;
			if($loop.isInnermost) {
				num_loops_innermost++;
				dowhile_innermost_loops++;
			}
		}

		if($loop.type == "foreach") {
			foreach_loops++;
			if($loop.isInnermost) {
				num_loops_innermost++;
				foreach_innermost_loops++;
			}
		}
		
		if($loop.nestedLevel > max_nest) {
			max_nest = $loop.nestedLevel;
			max_nest_location = $file.name + ' : ' + $class.name +'.' + $method.name + ' : ' + $loop.line;
		}
	end
	
	

	
	/****************** PRINT GENERAL INFORMATION *****************/

	println('\n\n\n-== [ General Information ] ==-\n');

	println("No. of Files: "+num_files);
	println("No. of Classes: "+num_classes);
	println("No. of Methods: "+num_methods);
	println("No. of Calls: "+num_calls);


	/****************** PRINT LOOP INFORMATION *******************/

	println('\n\n\n-== [ Loop Information ] ==-\n');
    
	var firstWidth = 10;
	var secondWidth = 8;
	var thirdWidth = 10;
	var totalWidth = firstWidth + secondWidth + thirdWidth + 2;


	println(lineWidth('=', totalWidth));
	println(withWidth('Type', firstWidth) + ' ' + withWidth('Total', secondWidth) + ' ' + withWidth('Innermost', thirdWidth));
	
	println(lineWidth('-', totalWidth));
	
	println(withWidth('For', firstWidth) + ' ' + withWidth(for_loops, secondWidth) + ' ' + withWidth(for_innermost_loops, thirdWidth));
	println(withWidth('While', firstWidth) + ' ' + withWidth(while_loops, secondWidth) + ' ' + withWidth(while_innermost_loops, thirdWidth));
	println(withWidth('Do-While', firstWidth) + ' ' + withWidth(dowhile_loops, secondWidth) + ' ' + withWidth(dowhile_innermost_loops, thirdWidth));
	println(withWidth('Foreach', firstWidth) + ' ' + withWidth(foreach_loops, secondWidth) + ' ' + withWidth(foreach_innermost_loops, thirdWidth));
	
	println(lineWidth('-', totalWidth));

	println(withWidth('Total', firstWidth) + ' ' + withWidth(num_loops, secondWidth) + ' ' + withWidth(num_loops_innermost, thirdWidth));

	println(lineWidth('=', totalWidth));

	println("\nLargest Loop Nest: "+ (max_nest+1) + ' @ ' + max_nest_location);
	
	/****************** PRINT CALL INFORMATION *******************/

	var mostCalled = '';
	var mostCalledCounter = -1;
	var mostCalls = '';
	var mostCallsCounter = -1;
	var largestName = 0;
	
	for (var funcName in callInfo) {

		if(funcName.length > largestName) largestName = funcName.length;

		var callee = callInfo[funcName]['callee'] || 0;
		var caller = callInfo[funcName]['caller'] || 0;

		if(callee > mostCalledCounter) {
			mostCalledCounter = callee;
			mostCalled = funcName;
		}

		if(caller > mostCallsCounter) {
			mostCallsCounter = caller;
			mostCalls = funcName;
		}
	}
	secondWidth = 10;
	thirdWidth = 10;
	totalWidth = largestName + secondWidth + thirdWidth + 2;

	println('\n\n\n-== [ Call Information ] ==-\n');

	println(lineWidth('=', totalWidth));
	println(withWidth('Name', largestName) + ' ' + withWidth('As Callee', secondWidth) + '  ' + withWidth('As Caller', thirdWidth));

	println(lineWidth('-', totalWidth));
	
	for (var funcName in callInfo) {

		var callee = callInfo[funcName]['callee'] || 0;
		var caller = callInfo[funcName]['caller'] || 0;

		if(funcName == mostCalled) callee += '*';
		if(funcName == mostCalls) caller += '*';

		println(withWidth(funcName, largestName) + ' ' + withWidth(callee, secondWidth) + '  ' + withWidth(caller, thirdWidth));
	}

	println(lineWidth('=', totalWidth));

	println("\n\nReport finished!");
end

/********************* HELPER methods *********************/
aspectdef Utils 
    static
        var withWidth = function(string, width) {

    		var diff = width - string.toString().length;
    		
    		if(diff <= 0) {
    			return string;
    		}
    
    		var result = '';
    		for (var i = 0; i < diff; i++) {
    
    			result += ' ';
    		}
    
    		return string + result;
    	};

	    var lineWidth = function(string, width) {

    		var result = '';
    		for (var i = 0; i < width; i++) {
    
    			result += string;
    		}
    
    		return result;
    	};
    end

end